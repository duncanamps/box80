//
// KEYFWD.Z80
//
// Takes keystrokes and forwards them to the screen
// Stop when we get a Ctrl+C
//
// Correctly handles:
//
//   1. Normal Windows file input where line is terminated with CR+LF, it will
//      send the CR+LF to the display
//	 2. UNIX files with lines terminated with LF only, it will send CR+LF to 
//      the display
//   3. Simple console input where the Enter key is pressed, lines are 
//      terminated with CR only which are then expanded to CR+LF correctly
//
// Duncan Munro
// 15/07/2023
//

			ORG		$0100			// CPM TPA start address
		
// BDOS calls
		
BDOS		EQU		$0005			// Call address for BDOS
BDOS_CONDIR	EQU		6				// Command code for direct console I/O
BDOS_STRING	EQU		9				// Command code to print a string

// ASCII characters

CTRL_C		EQU		3
LF			EQU		10
CR			EQU		13


START:		LD		C,BDOS_STRING	// String output
			LD		DE,WELCOME		// Welcome message
			CALL	BDOS			// Print it
			
LOOP:		LD		C,BDOS_CONDIR	// Command code for direct console I/O
			LD		E,$FF			// FF=Input character			
			CALL	BDOS			// Get the character in
			OR		A,A				// Zero ?
			JR		Z,LOOP			// Yes, go back for more
			CP		A,CTRL_C		// Ctrl-C ?
			JR		Z,EXIT			// Yes, goodbye
			;
			; Check character and previous character to determine what to
			; do next
			;
			LD		[CURRCHR],A		// Save current character
			CP		A,LF			// Line feed ?
			JR		Z,DO_LF			// Handle if so
			CP		A,CR			// Carriage Return ?
			JR		Z,PRINT			// Yest go and print
			; So it's not NUL, CTRL_C, CR, or LF
			LD		A,[PREVCHR]		// Check previous character
			CP		A,CR			// Carriage return ?
			JR		NZ,PRINT		// No, print as normal
			; We have a CR which is not followed by LF
			LD		C,BDOS_CONDIR	// Command code for direct console I/O
			LD		E,LF			// Get a LF as the character to print
			CALL	BDOS			// Print the LF
PRINT:		LD		C,BDOS_CONDIR	// Command code for direct console I/O
			LD		A,[CURRCHR]		// Get current character back
			LD		[PREVCHR],A		// Save it
			LD		E,A				// Copy character into E
			CALL	BDOS			// and print it
			JR		LOOP			// Go back for more
			
DO_LF:		LD		A,[PREVCHR]		// Get previous character
			CP		A,CR			// Carriage Return ?
			JR		Z,PRINT			// Yes, all good, go and print the LF
			; If we got here, there's a LF with no preceding CR (UNIX style)
			LD		C,BDOS_CONDIR	// Command code for direct console I/O
			LD		E,CR			// Get a CR as the character to print
			CALL	BDOS			// Print the CR
			JR		PRINT			// Now it's OK to print the LF
			
EXIT:		LD		C,BDOS_STRING	// String output
			LD		DE,GOODBYE		// Goodbye message
			JP		BDOS			// Print it and return to CP/M
			
// Variables

PREVCHR		DB		0				// Last character we input			
CURRCHR		DB		0				// Current character
			
// Dollar terminated strings
			
WELCOME:	DB		13,10
			DB		"CHARACTER ECHO",13,10	
			DB		"Use Ctrl+C to terminate",13,10
			DB		"Duncan Munro - 15/07/2023",13,10
			DB		"$"
GOODBYE:	DB		13,10
			DB		"Done",13,10	
			DB		"$"
			
			
			END
